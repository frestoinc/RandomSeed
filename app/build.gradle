apply plugin: 'com.android.application'
sourceSets { main { java.srcDirs = ['src/main/java'] } }
android {
    sourceSets { main { java.srcDirs = ['src/main/java'] } }
    compileSdkVersion 29
    buildToolsVersion "29.0.1"
    defaultConfig {
        applicationId "com.example.randomseed"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug{
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'InvalidPackage'
        abortOnError false
        absolutePaths true
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        fatal 'NewApi'
        error 'Wakelock', 'TextViewEdits'
        warning 'ResourceAsColor'
        checkDependencies true
    }
}

apply plugin: 'org.owasp.dependencycheck'

apply plugin: 'checkstyle'

checkstyle {
    toolVersion = '8.24'
}

task checkstyle(type: Checkstyle) {
    dependsOn 'assembleDebug'
    // rules.xml copy from:
    // https://raw.githubusercontent.com/checkstyle/checkstyle/checkstyle-7.4/src/main/resources/google_checks.xml
    // the version should be as same as plugin version
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    classpath = files()
}

apply plugin: 'pmd'

task pmd(type: Pmd) {
    ruleSetFiles = files("${project.rootDir}/config/pmd/rulespmd.xml")
    ignoreFailures = true
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/model/**'

    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination file("$project.buildDir/outputs/pmd/pmd.html")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'
}
